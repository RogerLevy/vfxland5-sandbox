\ =============================================================================
\ Bitmap Editor (Painter) - spawned from %SPRITE context menu
\ =============================================================================

REQUIRE %idir%/gui.vfx
REQUIRE %idir%/shapes.vfx
REQUIRE %vfxland5%/supershow/bitmap.vfx
REQUIRE %idir%/flood.vfx

BITMAP %idir%/painter-cursors.png
painter-cursors.png 32 32 TILESET-FROM painter-cursors.ts

variable fgc

: bresenham-line  ( srcx srcy destx desty xt - ) ( - )
    {: srcx srcy destx desty xt
        | dx dy sx sy err x y e2 :}

    srcx to x
    srcy to y

    destx srcx - abs to dx
    desty srcy - abs to dy

    srcx destx < if  1 else -1 then to sx
    srcy desty < if  1 else -1 then to sy

    dx dy - to err

    begin
        x y at xt execute

        x destx = y desty = and if exit then

        err 2 * to e2

        e2 dy negate >= if
            err dy - to err
            x sx + to x
        then

        e2 dx <= if
            err dx + to err
            y sy + to y
        then
    again
;

: revert-bmp ( bmpid - )
    {: bmpid | temp-albmp :}
    bmpid bitmap-file expand >zpad al_load_bitmap
    dup 0= abort" Failed to load bitmap file"
    to temp-albmp
    bmpid id>albmp al_set_target_bitmap
    get-blender replace-mode
        bmpid is-subbmp? if
            \ Sub-bitmap: copy just the region
            temp-albmp
            bmpid bmpxy 2s>f 
            bmpid bmpwh 2s>f
            0e 0e 0 al_draw_bitmap_region
        else
            \ Regular bitmap: copy over
            temp-albmp 0e 0e 0 al_draw_bitmap
        then 
    set-blender
    temp-albmp al_destroy_bitmap 
    display al_get_backbuffer al_set_target_bitmap ;

: revert, ( - )
    you [: me revert ;] s" Revert" option ;

class: %painter
    %ui-box derive

    prop sw-albmp :addr         \ Software bitmap (memory) for editing
    prop hw-bmp <readonly       \ Hardware bitmap ID in registry
    prop zm <save
    prop editable <save
    prop d :fixed <save                \ Brush diameter
    prop bgc <save

    template { 8 zm ! 1. d ! $FFFFFFFF fgc ! editable on  $808080e0 bgc ! }

    :: cursor-bmp ( - bmp ) 
        using? if  
            rmb held?  
            editable @ not or if 
                painter-cursors.ts 1 tile 
                exit
            then
        then
        painter-cursors.ts 0 tile ;

    || : update-box
        \ Set size based on bitmap dimensions * zm
        sw-albmp @ dup albmpw swap albmph zm @ dup 2* 2>p w 2! ;

    \ Initialize from a bitmap ID
    : init-sw-albmp ( bmpid - )
        sw-albmp @ ?dup if al_destroy_bitmap then  \ Free old bitmap
        dup hw-bmp !
        id>albmp ?dup -exit
        ALLEGRO_MEMORY_BITMAP al_set_new_bitmap_flags
        al_clone_bitmap sw-albmp !
        0 al_set_new_bitmap_flags
        update-box ;
        \ 8 zm !
        \ hw-bmp @ bmpwh max 48 >= if 4 zm ! then
        \ hw-bmp @ bmpwh max 96 >= if 3 zm ! then ;

    \ Sync software -> hardware
    || : sync ( - )
        hw-bmp @ id>albmp ?dup -exit
        al_set_target_bitmap
        get-blender replace-mode
            sw-albmp @ 0e 0e 0 al_draw_bitmap
        set-blender  \ Restore old blending mode
        display al_get_backbuffer al_set_target_bitmap ;

    \ Revert to disk version
    :: revert ( - )
        hw-bmp @ revert-bmp
        hw-bmp @ init-sw-albmp ;

    \ Navigate bitmaps in registry
    || : ?*  shift? if 10 * then ;
    : prev-bitmap ( - ) hw-bmp @ 1 ?* - 1 max init-sw-albmp ;
    : next-bitmap ( - ) hw-bmp @ 1 ?* + init-sw-albmp ;

    :: _draw
        bgc @ rgba8
        w 2@ rectf 
        sw-albmp @ if
            m{ at@p
                abspos 2p>f zm @ s>f fdup 0e transform
                0 0 at hw-bmp @ put
            atp m}
        then
        grey w 2@ rect 
        4 -6 +at  fgc @ rgba8  5. circf  grey 5. circ ;

    \ Mouse -> bitmap coordinates
    : mouse>bmp ( - x. y. )
        pmouse abspos 2- zm @ >p dup 2p/ 2pfloor ;

    \ very raggedy:
    \ || : stroke ( - )
    \     sw-albmp @ -exit
    \     sw-albmp @ al_set_target_bitmap
    \     get-blender replace-mode
    \         mouse>bmp 2>i at fgc @ rgba8 
    \         mickey at@ 2+ 2>p line
    \     set-blender
    \     display al_get_backbuffer al_set_target_bitmap
    \     sync ;

    \ less raggedy:
    \ || : stroke ( - )
    \     sw-albmp @ -exit
    \     e{
    \         sw-albmp @ al_set_target_bitmap
    \         1. thick
    \         get-blender replace-mode
    \             fgc @ rgba8
    \             mouse>bmp 2>i at
    \             at@ 2>p mickey 2>p zm @ dup 2/ 2-  line
    \         set-blender
    \         display al_get_backbuffer al_set_target_bitmap
    \         sync
    \     e} ;

    || 0 value lastx
    || 0 value lasty

    :: _mousedown ( btn - )
        lmb = -exit  editable @ if 
            me focus 
            mouse>bmp  to lasty to lastx 
        then ;

    : premul
        ppenr@ ppena@ p* ppenr! 
        ppeng@ ppena@ p* ppeng! 
        ppenb@ ppena@ p* ppenb! ;

    || 0 value rad

    || : odot  0.5 0.5 +atp rad circf ;
    || : dot  rad circf ;
    || : odd?  1.0 and 0<> ;
    || : touch  hw-bmp @ id>bmp >touched on ;

    || : stroke ( - )
        sw-albmp @ -exit
        e{
            sw-albmp @ al_set_target_bitmap 
            -1.0 thick
            
            get-blender fgc @ $ff and if premul-mode else replace-mode then
                fgc @ rgba8
                d @ 2 / to rad
                lastx lasty 2>i mouse>bmp 2>i 
                    d @ odd? if ['] odot else ['] dot then bresenham-line
                \ mouse>bmp 2>i at  white see-thru premul 8. circf
            set-blender
            display al_get_backbuffer al_set_target_bitmap
            sync touch
        e} ;

    || : flood ( - )
        sw-albmp @ -exit
        sw-albmp @ al_set_target_bitmap
        get-blender replace-mode
            mouse>bmp atp fgc @ rgba8 floodfill
        set-blender
        display al_get_backbuffer al_set_target_bitmap
        sync touch ;

    : eyedrop ( - )
        {: | fcolor[ 16 ] :}
        sw-albmp @ -exit
        fcolor[ sw-albmp @ mouse>bmp 2>i al_get_pixel
        fcolor[ 4f@ f>rgba8 fgc ! ;
    
    :: _mousewheel ( n - )
        {: n | old. rx. ry. factor. :}
        me hovered = -exit
        zm @ >p to old.
        n zm @ + 1 16 clamp zm !
        pmouse abspos 2- to ry. to rx.
        1.0 zm @ >p old. p/ - to factor.
        rx. factor. p* ry. factor. p* x 2+! ;

    || : ?mouse
        editable @ if me focused = -exit then
        me hovered = -exit
        lmb held? <space> held? not and if 
            editable @ if stroke else eyedrop then 
        then
        rmb held? shift? not and if eyedrop then 
        editable @ if
            mouse>bmp to lasty to lastx 
        then ;

    :: _process
        update-box
        using? -exit ?mouse ;

    :: _keydown ( key - )
        using? not if drop exit then
        \ dup <s> = ctrl? and if drop save-bitmap exit then
        dup <z> = ctrl? and if drop me revert exit then
        dup <f> = if flood then
        drop ;

    :: _keychar ( key - )
        using? not if drop exit then
        dup <,> = if drop prev-bitmap exit then
        dup <.> = if drop next-bitmap exit then
        drop ;

    :: delete
        sw-albmp @ ?dup if al_destroy_bitmap then
        element:delete ;

    :: on-attach
        parent @ %sprite is? -exit
        parent @ >bmp @ init-sw-albmp ;

    :: forth@ ( - a len )
        hw-bmp @ id>bmp body>name f" %s init-sw-albmp" ;

    || : clear, ( - )
        you [:
            sw-albmp @ al_set_target_bitmap
            black 0 ppena! screen
            display al_get_backbuffer al_set_target_bitmap
            sync
        ;] s" Clear" option ;

    :: _context-menu ( menu - )
        as>
        revert,
        clear,
        common-options, ;

    \ :: save ( - )
    \     touched @ -exit touched off save-bitmap ;

class;

\ Factory: create editor for a bitmap ID
: *paint ( bmpid - editor )
    %painter *add { init-sw-albmp me } ;

\ -- Add "Paint" to %SPRITE context menu --------------------------------------

: paint, ( - )
    you [:
        bmp @ *paint {
            you click-dims 2 -2 2/ x 2!
            corral
        }
    ;] s" Paint" option ;

\ Recursively refresh painters editing a given bitmap
: (refresh-painters) ( bmpid element - bmpid )
    [ here ]
    { 
        me %painter is? if dup hw-bmp @ = if hw-bmp @ init-sw-albmp then then 
        literal me each 
    } ;

: refresh-painters ( bmpid - )
    session ?dup if (refresh-painters) then drop ;

%sprite :: revert ( - )
    bmp @ dup revert-bmp refresh-painters ;

%sprite :: _context-menu ( menu - )
    as>
    paint,
    revert,
    common-options, ;
