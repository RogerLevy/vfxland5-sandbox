\ =============================================================================
\ Bitmap Editor (Painter) - spawned from %SPRITE context menu
\ =============================================================================

REQUIRE %idir%/gui.vfx
REQUIRE %idir%/shapes.vfx
REQUIRE %vfxland5%/supershow/bitmap.vfx
REQUIRE %idir%/flood.vfx

bitmap painter-cursors.png
painter-cursors.png 32 32 tileset-from painter-cursors.ts

variable fgc

class: %painter
    %ui-box derive

    prop sw-albmp :addr         \ Software bitmap (memory) for editing
    prop hw-bmp <readonly       \ Hardware bitmap ID in registry
    prop zm <save
    \ prop fgc <save

    template { 8 zm ! $FFFFFFFF fgc ! }

    :: cursor-bmp ( - bmp ) 
        using?   rmb held? if 
            painter-cursors.ts 1 tile 
            exit
        then
        painter-cursors.ts 0 tile ;

    || : update-box
        \ Set size based on bitmap dimensions * zm
        sw-albmp @ dup albmpw swap albmph zm @ dup 2* 2>p w 2! ;

    \ Initialize from a bitmap ID
    : init-sw-albmp ( bmpid - )
        sw-albmp @ ?dup if al_destroy_bitmap then  \ Free old bitmap
        dup hw-bmp !
        id>albmp ?dup -exit
        ALLEGRO_MEMORY_BITMAP al_set_new_bitmap_flags
        al_clone_bitmap sw-albmp !
        0 al_set_new_bitmap_flags
        update-box 
        8 zm !
        hw-bmp @ bmpwh max 48 >= if 4 zm ! then
        hw-bmp @ bmpwh max 96 >= if 3 zm ! then ;

    : get-blender  0 0 0 sp@ dup cell+ dup cell+ swap rot al_get_blender ;
    : set-blender  al_set_blender ;
    : replace-mode  ALLEGRO_ADD ALLEGRO_ONE ALLEGRO_ZERO set-blender ;

    \ Sync software -> hardware
    || : sync ( - )
        hw-bmp @ id>albmp ?dup -exit
        al_set_target_bitmap
        get-blender replace-mode
            sw-albmp @ 0e 0e 0 al_draw_bitmap
        set-blender  \ Restore old blending mode
        display al_get_backbuffer al_set_target_bitmap ;

    \ Navigate bitmaps in registry
    : prev-bitmap ( - ) hw-bmp @ 1 - 1 max init-sw-albmp ;
    : next-bitmap ( - ) hw-bmp @ 1 + init-sw-albmp ;

    \ Save to disk
    : save-bitmap ( - )
        sync
        hw-bmp @ bitmap-file expand >zpad hw-bmp @ ?bmp>parent id>albmp al_save_bitmap 
            0= abort" Error saving bitmap" 
        hw-bmp @ bitmap-file f" Saved bitmap to %s" type cr ;

    :: _draw
        black see-thru w 2@ rectf 
        sw-albmp @ if
            m{ at@p
                abspos 2p>f zm @ s>f fdup 0e transform
                0 0 at hw-bmp @ put
            atp m}
        then
        grey w 2@ rect 
        4 -6 +at  fgc @ rgba8  5. 5. circf  grey 5. 5. circ ;

    :: _mousedown ( btn - )
        drop  me focus ;

    \ Mouse -> bitmap coordinates
    : mouse>bmp ( - x. y. )
        mouse 2>p abspos 2- zm @ dup 2/ ;

    || : dot ( - )
        sw-albmp @ -exit
        sw-albmp @ al_set_target_bitmap
        get-blender replace-mode
            mouse>bmp at fgc @ rgba8 pixel
        set-blender
        display al_get_backbuffer al_set_target_bitmap
        sync ;

    \ very raggedy:
    \ || : stroke ( - )
    \     sw-albmp @ -exit
    \     sw-albmp @ al_set_target_bitmap
    \     get-blender replace-mode
    \         mouse>bmp 2>i at fgc @ rgba8 
    \         mickey at@ 2+ 2>p line
    \     set-blender
    \     display al_get_backbuffer al_set_target_bitmap
    \     sync ;

    \ less raggedy:
    \ || : stroke ( - )
    \     sw-albmp @ -exit
    \     e{
    \         sw-albmp @ al_set_target_bitmap
    \         1. thick
    \         get-blender replace-mode
    \             fgc @ rgba8
    \             mouse>bmp 2>i at
    \             at@ 2>p mickey 2>p zm @ dup 2/ 2-  line
    \         set-blender
    \         display al_get_backbuffer al_set_target_bitmap
    \         sync
    \     e} ;

    || : stroke ( - )
        sw-albmp @ -exit
        e{
            sw-albmp @ al_set_target_bitmap
            1. thick
            get-blender replace-mode
                fgc @ rgba8
                mouse>bmp atp
                at@p mickey 2>p zm @ dup 2/ 2-  line
            set-blender
            display al_get_backbuffer al_set_target_bitmap
            sync
        e} ;

    || : flood ( - )
        sw-albmp @ -exit
        sw-albmp @ al_set_target_bitmap
        get-blender replace-mode
            mouse>bmp atp fgc @ rgba8 floodfill
        set-blender
        display al_get_backbuffer al_set_target_bitmap
        sync ;

    : f>rgba8 ( f:r f:g f:b f:a - n )
        255e f* f>s 
        255e f* f>s 8 << or
        255e f* f>s 16 << or
        255e f* f>s 24 << or ;

    : eyedrop ( - )
        {: | fcolor[ 16 ] :}
        sw-albmp @ -exit
        fcolor[ sw-albmp @ mouse>bmp 2>i al_get_pixel 
        fcolor[ 4f@ f>rgba8 fgc ! ;

    || : ?mouse
        me hovered = -exit
        lmb held? <space> held? not and if stroke then 
        rmb held? shift? not and if eyedrop then ;

    :: _process
        update-box
        using? -exit ?mouse ;

    :: _keydown ( key - )
        using? not if drop exit then
        dup <s> = ctrl? and if drop save-bitmap exit then
        dup <f> = if flood then
        drop ;

    :: _keychar ( key - )
        using? not if drop exit then
        dup <,> = if drop prev-bitmap exit then
        dup <.> = if drop next-bitmap exit then
        drop ;

    :: delete
        sw-albmp @ ?dup if al_destroy_bitmap then
        element:delete ;

    :: on-attach
        parent @ %sprite is? -exit
        parent @ >bmp @ init-sw-albmp ;

    :: forth@ 
        hw-bmp @ id>bmp body>name f" %s init-sw-albmp" ;

    || : clear, ( - )
        you [:
            sw-albmp @ al_set_target_bitmap 
            black 0 ppena! screen
            display al_get_backbuffer al_set_target_bitmap 
            sync
        ;] s" Clear" option ;

    :: _context-menu ( menu - )
        as>
        clear, 
        common-options, ;

class;

\ Factory: create editor for a bitmap ID
: *paint ( bmpid - editor )
    %painter add { init-sw-albmp me } ;

\ -- Add "Paint" to %SPRITE context menu --------------------------------------

: paint, ( - )
    you [:
        bmp @ *paint {
            you click-dims 2 -2 2/ x 2!
        }
    ;] s" Paint" option ;

%sprite :: _context-menu ( menu - )
    as>
    paint,
    common-options, ;
