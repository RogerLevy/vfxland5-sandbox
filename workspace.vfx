\ ==============================================================================
\ Workspace IDE
\ ==============================================================================

\ -- Dependencies --------------------------------------------------------------

REQUIRE %vfxland5%/forge/lib/dltree.vfx
REQUIRE %vfxland5%/supershow/traits.vfx
REQUIRE %vfxland5%/supershow/bitmap.vfx
REQUIRE %vfxland5%/supershow/sprites.vfx
REQUIRE %vfxland5%/supershow/tileset.vfx
REQUIRE %vfxland5%/supershow/autoload.vfx
REQUIRE %idir%/shapes.vfx
REQUIRE %idir%/arrow.vfx
REQUIRE %idir%/colors.vfx
\ REQUIRE %idir%/gui.vfx    \ For now, rely on micros.vfx to load this due to a current limitation of NIBS
REQUIRE %idir%/micros.vfx
REQUIRE %idir%/painter.vfx
REQUIRE %idir%/togglebox.vfx

\ -- Bookmark -----------------------------------------------------------------

class: %bookmark
    %label derive
    prop dx :fixed <save
    prop dy :fixed <save

    || 2. constant m

    :: click-box@ ( - x1 y1 x2 y2 )
        abspos m m 2- w 2@ m m 2 2 2* 2+ box ;

    :: _draw
        _update-box
        m >i negate dup +at
        light-green w 2@ m m 2 2 2* 2+ rectf
        black w 2@ m m 2 2 2* 2+ rect
        m m 2>i +at
        black _draw-text ;
    
    :: _mousedown ( btn - )
        lmb = -exit
        bolted? not shift? not and ?exit
        dx 2@ desktop >x 2! ;

    : bookmark,
        you [:
            desktop -> x 2@ dx 2!
        ;] s" Bookmark" option ;

    :: _context-menu ( menu - )
        as>
        bookmark,
        common-options, ;
class;

\ -- Desktop HUD --------------------------------------------------------------

: dhud ( - desktop-hud )
    desktop ?dup if
        >highest @ dup %desktop-hud is? ?exit 
        drop
    then
    %desktop-hud make dup desktop push ;

\ -- Main Loop ----------------------------------------------------------------

: ?save  <s> pressed? ctrl? and -exit save-all save-bitmaps ;

: workspace
    display al_hide_mouse_cursor
    2 user-zoom !
    show> 
        session ?dup if 0 0 at draw then
        0 0 at session gui-overlay 
        draw-cursor 
    work> 
        get-mouse 
        desktop ?dup if decay-gui then
        session ?dup if process-gui then 
        ?save
;


workspace

\ -- Misc ---------------------------------------------------------------------

[:
    default-sticky,
    you { bolted? } ?exit
    you >parent @ hud = ?exit
    you [: abspos me dhud push x 2! ;] s" Desktop Sticky" option
;] is sticky,

[:
    dup hud descendant? if
        {
            desktop relpos
            me desktop push
            x 2!
        }
    else 
        drop
    then
;] is cloned

: wide
    display ALLEGRO_FRAMELESS true al_set_display_flag drop
    display -1920 0 al_set_window_position
    display ALLEGRO_MAXIMIZED true al_set_display_flag drop
    display 1920 2 * 1060 al_resize_display drop
    display ALLEGRO_FRAMELESS true al_set_display_flag drop
;


\ -- Instructions---------------------------------------------------------------

cr
cr
." Workspace Controls: " cr
."  Tab - Toggle using/designing mode (hold+click for temporary toggle)" cr
."  Tilde - Toggle the global HUD" cr
."  LMB - designing: select, drag   using: use" cr
."  Ctrl + LMB - clone" cr
."  Shift + LMB - designing: use  (deprecated)" cr
."  RMB - designing: context menu" cr
."  Shift+RMB - designing: inspect" cr
."  Del - designing: delete selected elements" cr
cr

