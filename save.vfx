\ =============================================================================
\ Save/Load System for Element Trees
\ =============================================================================

require %vfxland5%/cjson/nibs-json2.vfx

also cjson2>>

\ -- Serialization ------------------------------------------------------------

\ Serialize element with children recursively
defer _element>json
: element>json ( element - cjson )
    {: el | cjson children-arr :}
    el *json to cjson

    \ Add children array if element has children
    el #children 0> if
        new-array to children-arr
        el >lowest @
        el #children for
            dup { temporary? } not if
                dup .summary cr
                dup _element>json 0 children-arr add-object
            then
            >next @
        loop
        drop
        children-arr "children" cjson add-array
    then
    cjson ;
['] element>json is _element>json

\ -- Deserialization ----------------------------------------------------------

\ Deserialize JSON to element, reconstructing hierarchy
defer _json>element
: json>element ( cjson - element )
    {: cjson :}

    \ Get class name and instantiate
    cjson "class" j>s evaluate make {
        \ Read properties into the new element
        cjson me read-props

        \ Cache the script field if there is one
        cjson "script" ?j>z ?dup if
            zcount *lstring script !
        then

        \ Recursively create children
        cjson "children" ?get-item ?dup if
            jcount for
                dup i get-item _json>element me push
            loop drop
        then
    me } ;

['] json>element is _json>element

\ -- High-level API -----------------------------------------------------------

\ Save element tree to JSON file
: save-tree ( element path len - )
    serjson> ( element json ) >r element>json "root" r> add-object ;

\ Load element tree from JSON file
: load-tree ( path len - element )
    load-json "root" get-item json>element ;
