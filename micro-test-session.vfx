: 1x  1 user-zoom ! ;
: 2x  2 user-zoom ! ;
: 3x  3 user-zoom ! ;

: desktop ( n - )
    0 9 clamp session el@ to machines ; 

: paint ( bmpid - )
    mouse at hud { *paint drop } ;

: *kpress ( key xt - ability )
    %ability add { kp ! k ! me } ;

c: %gravity %move [non-decaying] ;
%gravity >template { 90. ang ! 0.03 acc ! }

: *sprite ( bmp - sprite )
    %sprite add { bmp ! me } ;

\ -- Viewport (experimental) ---------------------------------------------------

class: %viewport %ui-box derive
    works-with %_micro
    prop xt :xt
    prop active <readonly

    :: on-focus  active on ;
    :: on-blur   active off ;

    :: _mousedown 
        lmb = -exit
        <space> held? ?exit
        me focus ;

    :: _draw
        abspos 2>i zoom @ dup 2*``
        w 2@ 2>i zoom @ dup 2*`` al_set_clipping_rectangle 
        cls 
        xt @ dup if catch dup if nip then then
        0 0 winw winh al_set_clipping_rectangle 
        throw ;

class;

: *viewport ( w h - viewport )
    %viewport add { 2>p w 2! me } ;

class: %owl
    %actor derive
class;


\ -- Bitmap Creation and Loading -----------------------------------------------

|| : center
    machines { x 2@ 2>i 2negate } 
    winw winh 2 2 2/ zoom @ dup 2/ 2+ ;

: newbmp ( w h - )
    {: | zbuf[ 1024 ] :}
    0 z" New Bitmap" z" *.png" ALLEGRO_FILECHOOSER_SAVE file-dialog
    dup if 
        -ext f" %s.png" zbuf[ zplace
        al_create_bitmap zbuf[ swap al_save_bitmap 0= abort" Bitmap file create fail!"
        zbuf[ f" bitmap %z" .evaluate
        
        zbuf[ zcount -path evaluate machines { 
            center at *sprite dup o. dup >bmp @ paint o. 
        }
    else  
        2drop
    then ;

: loadbmp ( - )
    {: | zbuf[ 1024 ] :}
    0 z" Load Bitmap" z" *.png;*.jpg;*.jpeg;*.bmp;*.tif;*.tiff;*.pcx;*.gif" ALLEGRO_FILECHOOSER_FILE_MUST_EXIST file-dialog
    dup if 
        zbuf[ zplace
        zbuf[ f" bitmap %z" .evaluate
        zbuf[ zcount -path evaluate machines { center at *sprite drop }
    else
        2drop 
    then ;

