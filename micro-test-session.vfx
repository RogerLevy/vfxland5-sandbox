: 2x  2 user-zoom ! ;
: 3x  3 user-zoom ! ;

: desktop ( n - )
    0 9 clamp session el@ to machines ; 

: paint ( bmpid - )
    mouse at hud { *paint drop } ;

: *kpress ( key xt - ability )
    %ability add { kp ! k ! me } ;

c: %gravity %move [non-decaying] ;
%gravity >template { 90. ang ! 0.03 acc ! }

: *sprite ( bmp - sprite )
    %sprite add { bmp ! me } ;

\ -- Viewport (experimental) ---------------------------------------------------

class: %viewport %ui-box derive
    works-with %_micro
    prop xt :xt
    prop active <readonly

    :: on-focus  active on ;
    :: on-blur   active off ;

    :: _mousedown 
        lmb = -exit
        <space> held? ?exit
        me focus ;

    :: _draw
        abspos 2>i zoom @ dup 2*``
        w 2@ 2>i zoom @ dup 2*`` al_set_clipping_rectangle 
        cls 
        xt @ dup if catch dup if nip then then
        0 0 winw winh al_set_clipping_rectangle 
        throw ;

class;

: *viewport ( w h - viewport )
    %viewport add { 2>p w 2! me } ;

class: %owl
    %actor derive
class;


\ -- Bitmap Creation and Loading -----------------------------------------------

: find-next-pic# ( - n )
    \ Find highest picNNN.png in current directory and return next number
    {: | buf[ 256 ] max :}
    0 to max
    z" pic*.png" [: ( zpath - )
        zcount -path buf[ place          \ Get just filename
        buf[ count s" pic" strin? not if drop exit then  \ Must start with "pic"
        buf[ 3 + c@  [char] 0 [char] 9 1+ within not if drop exit then  \ First digit must be 0-9
        buf[ count 3 - 3 buf[ 3 + digit-string? if   \ Parse 3-digit number
            max max to max
        else drop then
    ;] find-files
    max 1 + ;

: newbmp ( w h - )
    {: w h | zbuf[ 1024 ] default[ 256 ] :}
    find-next-pic# f" pic%03d.png" default[ place
    default[ count z>zbuf zbuf z" New Bitmap" z" *.png" ALLEGRO_FILECHOOSER_SAVE file-dialog
    ?dup if
        -ext f" %s.png" zbuf zplace
        w h al_create_bitmap zbuf swap al_save_bitmap 0= abort" Bitmap file create fail!"
        zbuf f" bitmap %z" .evaluate
        machines { abspos 2>i 2negate }
        winw winh 2 2 2/ zoom @ dup 2/
        2+ at
        zbuf zcount -path evaluate machines { *sprite >bmp @ paint }
    then ;

: loadbmp ( - )
    {: | zbuf[ 1024 ] :}
    0 z" Load Bitmap" z" *.png;*.jpg;*.jpeg;*.bmp;*.tif;*.tiff;*.pcx;*.gif" ALLEGRO_FILECHOOSER_FILE_MUST_EXIST file-dialog
    ?dup if 
        zbuf[ zplace
        zbuf[ f" bitmap %z" .evaluate
        machines { abspos 2>i 2negate } 
        winw winh 2 2 2/ zoom @ dup 2/  
        2+ at 
        zbuf[ zcount -path evaluate machines { *sprite drop }
    else
        2drop
    then ;

