public

borrow nib2>> prop+

class: %inspector 
    %panel derive
    prop obj :ref %object <readonly
    template { $2070e0d0 clr ! }

    || : obj@  obj @ ?dup 0= if parent @ then ;

    :: _draw  
        panel:_draw
        obj@ ?dup if 
            dup %_element is? if
                white-connector
            else drop then
        then ;

    :: on-attach
        {: | obj0 n property0 type0 readonly? :}

        ['] delete me each

        obj@ to obj0

        0 0 at
        obj0 >cla @ body>name *label { +bolted editable off $ffffffff clr ! }

        0 to n
        obj0 >cla @ -> property-list list-count for
            list-next to property0
            property0 obj0 >cla @ >property-type @ to type0
            property0 obj0 >cla @ >readonly-flag @ to readonly?

            type0 TYPE_INT =
            type0 TYPE_CELL = or
            type0 TYPE_FIXED = or
            property0 obj0 >cla @ >property-size @ cell <= and if
                \ create label with property name
                property0 body>name f" %s" *label {
                    +bolted
                    editable off
                    0. n 1 + 9 * >p x 2!
                    readonly? if valof dark-yellow else valof yellow then clr !
                }

                \ create number widget (editable or readonly)
                type0 TYPE_FIXED = if
                    0 *fixed
                else
                    0 *int
                then {
                    +bolted
                    100. n 1 + 9 * >p x 2!
                    obj0 property0 prop+ ra !
                    readonly? not editable !
                    readonly? if $a0a0a0ff clr ! then
                }

                1 +to n
            then

            type0 TYPE_CSTRING =
            type0 TYPE_LSTRING = or if
                \ create label with property name
                property0 body>name f" %s" *label {
                    +bolted
                    editable off
                    0. n 1 + 9 * >p x 2!
                    readonly? if valof dark-yellow else valof yellow then clr !
                }

                \ create string field widget
                type0 *stringfield {
                    +bolted
                    100. n 1 + 9 * >p x 2!
                    obj0 property0 prop+ ra !
                    readonly? not editable !
                    readonly? if $a0a0a0ff clr ! then
                }

                1 +to n
            then
        loop drop
        150. n 1 + 9 * >p w 2! ;

class;

: *inspector ( object - inspector )
    %inspector *add { obj !
        obj @ %_element is? if
            \ position to the left of the element
            obj @ { abspos } w @ 8. + 0 2- x 2!
        else
            mouse 2>p x 2!
        then
        corral
    me } ;

|| 0 value obj0
: find-inspector ( object - inspector|0 )
    to obj0
    0 [: { me %inspector is? if me >obj @ obj0 = if drop me then then } ;] session each ;

: inspect ( object - )
    {: obj :}
    obj %inspector is? ?exit
    obj find-inspector ?dup if unload then
    session { obj *inspector { +temporary } } ;

: toggle-inspector ( object - )
    dup find-inspector ?dup if
        nip unload 
    else
        inspect
    then ;