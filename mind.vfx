REQUIRE %idir%/element.vfx
REQUIRE %idir%/save.vfx
REQUIRE %vfxland5%/supershow/stage.vfx

class: %actor
    prop mind :ref %element         \ points to micro machine root
    prop mindsrc :lstring <save     \ template file: "enemy-ai.json"
class;

class: %stage
    prop flyloft :ref %element          \ scene GUI - minds, notes, debug viz
class;

%actor :: unload
    mind @ ?dup if unload then  \ unload mind tree when actor dies
    actor:unload ;

class: %element
    prop bod :ref %actor
class;

%element :: delete
    bod @ ?dup if >mind off then  
    element:delete ;

: it's-alive ( element - ) mind ! me swap >bod ! ;
: >stage ( actor - stage ) >parent @ ;
: my-flyloft ( - element ) me >stage >flyloft @ ;

: init-mind ( - ) 
    \ load mind template for current actor
    my-flyloft -exit  \ no flyloft for the stage
    mind @ -exit  \ if mind already exists (from save state), skip
    mindsrc @ ?dup if
        lcount load-tree   dup my-flyloft push   it's-alive
    then ; 

: hmake  e{ in-heap make e} ;

: *mind ( class - object )
    \ create a mind for current actor through code
    my-flyloft 0= abort" No flyloft for current actor's stage!"
    mind @ ?dup if unload then  \ if mind already exists, delete
    hmake dup my-flyloft push   dup it's-alive ;


