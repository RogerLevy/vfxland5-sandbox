REQUIRE %vfxland5%/forge/lib/dltree.vfx
REQUIRE %vfxland5%/cjson/cjson2.vfx
REQUIRE %vfxland5%/supershow/traits.vfx

ECONTEXT gui-ec
0 value hovered
0 value focused   \ Element with keyboard focus
1024 %addr stack selection

redef : *lstring ( a n - a' )
    dup cell+ 256 max allocate throw >r r@ lplace r> ;

\ -- Base Trait ----------------------------------------------------------------

trait: %_element
    is-a %dltree
    is-a %drawable
    is-a %unloadable
    is-a %xy
    prop flags <save <readonly
    prop script :lstring 
    :: click-box@ ( - x1. y1. x2. y2. ) $DEADBEEF dup dup dup ;
    :: script@ ( - a len ) script @ ?dup if lcount else s" " then ;
    \ :: _click ( n - ) drop ;
    :: _enter ( - ) ;
    :: _leave ( - ) ;
    :: _mousedown ( btn - ) drop ;  \ lbm , rmb , or mmb
    \ :: _mouseup ( btn - ) drop ;
    \ :: _mousemove ( - ) ;
    :: _process ( - ) ;
    :: _draw ( - ) ;
    :: delete ( - ) stub ;
    \ :: draggable? ( - flag ) true ;
    :: active? ( - flag ) true ;
    :: _context-menu ( menu - ) drop ;   \ override to add items / customize the menu
    :: on-clone ( - ) ;
    :: on-attach ( - ) ;
    :: on-focus ( - ) ;       \ Called when element gains keyboard focus
    :: on-blur ( - ) ;        \ Called when element loses keyboard focus
    :: _keydown ( key - ) drop ;  \ Single key press (no repeat)
    :: _keychar ( key - ) drop ;  \ Key with repeat
trait;

\ -- Helpers -------------------------------------------------------------------

: +unloaded  0 flags set-bit ;
: unloaded?  0 flags test-bit ;
: +bolted    1 flags set-bit ;
: -bolted    1 flags reset-bit ;
: bolted?    1 flags test-bit ;

: abspos  x 2@ me begin >parent @ ?dup while { x 2@ 2+ me } repeat ;
: at-me  abspos atp ;
: box  2over 2+ ;
: ?push  2dup contains? if 2drop exit then push ;
: see-thru   0.5 ppena! ;
: click-dims  click-box@ 2swap 2- ;

: load-script ( path len - )
    expand slashes >pad count included ;

: memoize-include ( <path> - )
    me %_element is? not abort" Current object is not an element."
    bl parse 2dup *lstring script !
    load-script ;

: >root ( element - root )
    begin dup >parent @ ?dup while nip repeat ;

: from-element ( absx absy element - )
    { abspos } 2>i 2- at ;

: relpos ( element - x. y. )
    { abspos } abspos 2swap 2- ;

\ -- Focus System --------------------------------------------------------------

: focus ( element - )
    focused ?dup if on-blur then
    dup to focused
    ?dup if on-focus then ;

: unfocus ( - ) 
    0 focus ;

\ -- Element Class -------------------------------------------------------------

class: %element
    is-a %_element
    template { gui-ec ecp! in-heap }
class;

%element :: .summary  
    object:.summary x 2p? ;

0 %element :construct ( - )
    at@p x 2!  gui-ec ecp! in-heap ;

: delete-element ( element - )
    dup hovered = if 0 to hovered then
    dup { ['] delete me each }   \ recursively delete children
    \ dup selection contains? if dup selection remove then
    dup >script @ free drop      \ free script lstring
    dup dlremove free drop ;

%element :: delete
    me focused = if 0 to focused then 
    me delete-element ;

\ -- Element Methods -----------------------------------------------------------

%element :: unload ( - )
    +unloaded 
    selection vacate 
    0 to hovered ;

defer _sweep
: sweep ( iterable - )
    [: dup _sweep { unloaded? if me delete then } ;] swap each ;
['] sweep is _sweep

%element :: vacate
    ['] unload me each  me sweep ;

%element :: push
    dup %_element is? not abort" ELEMENT:PUSH : Source is not an element."
    dup dltree:push on-attach ;

%element :: insert-under
    dup %_element is? not abort" ELEMENT:INSERT-UNDER : Source is not an element."
    dltree:insert-under ;

%element :: insert-over
    dup %_element is? not abort" ELEMENT:INSERT-OVER : Source is not an element."
    dltree:insert-over ;

%element :: unshift
    dup %_element is? not abort" ELEMENT:UNSHIFT : Source is not an element."
    dltree:unshift ;

defer __process
: process ( element - )
    dup active? not if drop exit then
    dup _process ['] __process swap each ;
['] process is __process

%element :: draw
    me active? not if exit then
    \ TODO: fix this workaround
    vga-8x8 fnt!

    at-me  me _draw ['] draw me each ;

\ -- Clone ---------------------------------------------------------------------

: clone ( element - element' )
    as> \ scope into object. establishes EC
        cla @ make {     \ create new instance of same class
            you me dup sizeof move  \ copy data
            0 dup dup dup dup
            parent ! prev ! next ! lowest ! highest !
            0 item-count !
            me on-clone
        me } ;

defer _deep-clone
: deep-clone ( element - element' )
    dup clone swap
    [: _deep-clone over push ;] swap each ;
' deep-clone is _deep-clone
