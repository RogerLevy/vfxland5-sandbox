\ -- Save/Load ----------------------------------------------------------------

also cjson2>>

: session-relpath  s" ." 2swap >relpath ;

: asset>json ( asset - json|0 )
    as>
    \ me .summary cr
    [defined] %bitmap [if]
        me %bitmap is? if 
            srcpath$ count session-relpath f" bitmap %s" 
            >zpad new-string exit 
        then
    [then]
    [defined] %tileset [if]
        me %tileset is? if 
            me body>name` tw 2@ swap`` bmp @ id>bmp body>name` f" %s %n %n tileset-from %s" 
            >zpad new-string exit 
        then 
    [then]
    [defined] %sample [if]
        me %sample is? if 
            srcpath$ count session-relpath f" sample %s" 
            >zpad new-string exit 
        then 
    [then]
    0  
;

: autoloads>json ( - json )
    new-array {: jarray :}
    autoloads list-count for
        list-next asset>json 0 jarray add-item
    loop drop 
    jarray ;

[undefined] sessionpath$ [if]
    cstring sessionpath$
    s" workspace.json" sessionpath$ place
[then]

: save-all 
    session sessionpath$ count  save-tree 
    sessionpath$ count load-json >r 
    autoloads>json "autoloads" r@ add-item 
    r> sessionpath$ count save-json 

    [: [ here ]   dup save   literal swap each ;] session each
;

: load-all
    sessionpath$ count load-json >r
    r@ "autoloads" ?j>j ?dup if
        [: j>s ['] .evaluate catch if 2drop then ;] jeach
    then
    r> destroy-json

    sessionpath$ count load-tree 
    \ session delete   \ TODO: commented out as workaround for instability
    to session 
;

: loadws
    {: | zbuf[ 1024 ] :}
    0 z" Load Workspace" z" *.json" ALLEGRO_FILECHOOSER_FILE_MUST_EXIST file-dialog
    dup if 
        save-all
        -ext f" %s.json" sessionpath$ place
        load-all
    else
        2drop 
    then ;
    
: savews
    {: | zbuf[ 1024 ] :}
    0 z" Save Workspace" z" *.json" ALLEGRO_FILECHOOSER_SAVE file-dialog
    dup if 
        -ext f" %s.json" sessionpath$ place
        save-all
    else  
        2drop
    then ;
