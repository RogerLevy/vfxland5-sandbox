class: %togglebox
    %label derive
    prop opn <save
    template { opn on }

    :: opened?  opn @ ;
    :: open     opn on ;
    :: close    opn off ;

    :: process
        me active? -exit
        me _process 
        opn @ if ['] process me each then ;
        
    :: draw
        me active? -exit
        at-me  me _draw 
        opn @ if ['] draw me each then ;

    :: _mousedown ( btn - )
        dup rmb = shift? and if 
            opn on
            drop exit
        then
        lmb = -exit
        <space> held? ?exit
        designing? shift? and 
        using? or 
        designing? bolted? and or -exit
        opn toggle ;

    || 4. constant m

    :: click-box@ ( - x1 y1 x2 y2 )
        abspos m m 2- w 2@ m m 2 2 2* 2+ box ;

    :: _draw
        _update-box  
        m >i negate dup +at
        white w 2@ m m 2 2 2* 2+ rectf 
        black w 2@ m m 2 2 2* 2+ rect 
        m m 2>i +at
        black _draw-text
        w @ 2 / 4. - h @ m + 2. - 2>i +at
        black 8. 4. rectf ;

class;

: *togglebox ( a len - togglebox )
    %togglebox add { me text! ( you { rgba8@ } clr ! ) me } ;